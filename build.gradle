import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.github.ben-manes.versions' version '0.47.0'
    id 'net.minecraftforge.gradleutils' version '2.+'
}

group = 'net.minecraftforge'
version = gradleutils.tagOffsetVersion
println "Version: $version"

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

license {
    header = file('LICENSE-header.txt')
    include 'net/minecraftforge/mergetool/**/*.java'
    newLine = false
}

sourceSets {
    cpwFML
    forgeFML
    forgeAPI
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes('Main-Class': 'net.minecraftforge.mergetool.ConsoleMerger')
        attributes('Implementation-Version': project.version)
    }
    from sourceSets.cpwFML.output
    from sourceSets.forgeFML.output
    from sourceSets.forgeAPI.output
}

tasks.named('shadowJar', ShadowJar).configure {
    archiveClassifier = 'fatjar'
}

tasks.named('sourcesJar', Jar).configure {
    from sourceSets.cpwFML.allSource
    from sourceSets.forgeFML.allSource
    from sourceSets.forgeAPI.allSource
}

tasks.register('cpwFML', Jar) {
    archiveClassifier = 'cpw'
    from sourceSets.cpwFML.output
}

tasks.register('cpwFMLSources', Jar) {
    from sourceSets.cpwFML.allSource
    archiveClassifier = 'sources'
    archiveBaseName = 'mergetool-cpw'
}

tasks.register('forgeFML', Jar) {
    archiveClassifier = 'forge'
    from sourceSets.forgeFML.output
}

tasks.register('forgeFMLSources', Jar) {
    from sourceSets.forgeFML.allSource
    archiveClassifier = 'sources'
    archiveBaseName = 'mergetool-fml'
}

tasks.register('forgeAPI', Jar) {
    manifest.attributes('Automatic-Module-Name': 'net.minecraftforge.mergetool.api')
    archiveClassifier = 'api'
    from sourceSets.forgeAPI.output
}

tasks.register('forgeAPISources', Jar) {
    from sourceSets.forgeAPI.allSource
    archiveClassifier = 'sources'
    archiveBaseName = 'mergetool-api'
}

dependencies {
    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.ow2.asm:asm-tree:9.5'
    implementation 'org.ow2.asm:asm-util:9.5'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'net.minecraftforge:srgutils:0.4.15'

    implementation sourceSets.cpwFML.output
    implementation sourceSets.forgeFML.output
    implementation sourceSets.forgeAPI.output
}

artifacts {
    archives shadowJar
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        artifactId = 'mergetool'

        pom {
            name = 'MergeTool'
            description = 'Merges two jar files together, useful for rebuilding Retroguard stripped jars.'
            url = 'https://github.com/MinecraftForge/MergeTool'
            
            PomUtils.setGitHubDetails(pom, 'MergeTool')

            license PomUtils.Licenses.LGPLv2_1

            developers {
                developer PomUtils.Developers.LexManos
            }
        }
    }
    
    publications.register('mavenJavaApi', MavenPublication) {
        artifact source: forgeAPI, classifier: ''
        artifact forgeAPISources

        artifactId = 'mergetool-api'
        version = '1.0'

        pom {
            name = 'MergeTool-API'
            description = 'API Markers for Merging Two jars together. See https://github.com/MinecraftForge/MergeTool'
            url = 'https://github.com/MinecraftForge/MergeTool'
            
            PomUtils.setGitHubDetails(pom, 'MergeTool')

            license PomUtils.Licenses.LGPLv2_1

            developers {
                developer PomUtils.Developers.LexManos
            }
        }
    }
    
    publications.register('mavenJavaCpw', MavenPublication) {
        artifact source: cpwFML, classifier: ''
        artifact cpwFMLSources

        artifactId = 'mergetool-cpw'
        version = '1.0'

        pom {
            name = 'MergeTool-cpw'
            description = 'API Markers for Merging Two jars together. See https://github.com/MinecraftForge/MergeTool'
            url = 'https://github.com/MinecraftForge/MergeTool'
            
            PomUtils.setGitHubDetails(pom, 'MergeTool')

            license PomUtils.Licenses.LGPLv2_1

            developers {
                developer PomUtils.Developers.cpw
            }
        }
    }
    
    publications.register('mavenJavaFML', MavenPublication) {
        artifact source: forgeFML, classifier: ''
        artifact forgeFMLSources

        artifactId = 'mergetool-fml'
        version = '1.0'

        pom {
            name = 'MergeTool-fml'
            description = 'API Markers for Merging Two jars together. See https://github.com/MinecraftForge/MergeTool'
            url = 'https://github.com/MinecraftForge/MergeTool'
            
            PomUtils.setGitHubDetails(pom, 'MergeTool')

            license PomUtils.Licenses.LGPLv2_1

            developers {
                developer PomUtils.Developers.cpw
            }
        }
    }
    
    
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}
